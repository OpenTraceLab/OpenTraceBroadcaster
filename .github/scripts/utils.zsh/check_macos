autoload -Uz is-at-least log_group log_info log_error log_status

log_info 'Checking for Homebrew...'
if (( ! ${+commands[brew]} )) {
  log_error 'No Homebrew command found. Please install Homebrew (https://brew.sh)'
  return 2
}

brew bundle --file ${SCRIPT_HOME}/.Brewfile
rehash

# Install OpenTraceCapture dependency
log_group 'Installing OpenTraceCapture...'
local api_response=$(curl -s https://api.github.com/repos/OpenTraceLab/OpenTraceCapture/releases)
if [[ -z "${api_response}" ]] || [[ "${api_response}" == "null" ]]; then
  log_error "Failed to fetch OpenTraceCapture releases from GitHub API"
  return 2
fi

local otc_latest_url=$(echo "${api_response}" | jq -r '.[0].assets[] | select(.name | test(".*macos.*\\.tar\\.gz$")) | .browser_download_url' | head -1)

if [[ -z "${otc_latest_url}" ]]; then
  log_error "Could not find OpenTraceCapture macOS release"
  return 2
fi

curl -L -o /tmp/opentracecapture-macos.tar.gz "${otc_latest_url}"
sudo tar -xzf /tmp/opentracecapture-macos.tar.gz -C /usr/local

log_group
